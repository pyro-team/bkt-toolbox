; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "BKT-Toolbox"
#define MyAppPublisher "Business Kasper"
#define MyAppURL "https://www.bkt-toolbox.de"
#define MyAppVersion "2.7.3"
#define MyReleaseDate "210129"
;GetDateTimeString('yymmdd', '', '');

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{BD924AD8-8870-46C1-AAE1-8999D7B18E51}
AppName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
DefaultDirName={localappdata}\BKT-Toolbox
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
; NOTE: DisableDirPage auto would not ask for location if previous installation is found
DisableDirPage=no
;DisableReadyPage=yes
OutputDir=_releases
OutputBaseFilename=bkt_install_r{#MyReleaseDate}
Compression=lzma
SolidCompression=yes
SourceDir=..\
PrivilegesRequired=lowest
CloseApplicationsFilter=*.exe,*.dll,*.pptx
;SignedUninstaller=yes
LicenseFile=setup\license.txt
WizardStyle=modern
SetupIconFile=setup\bkt_logo.ico
WizardSmallImageFile=setup\bkt_logo_55x55.bmp,setup\bkt_logo_64x68.bmp,setup\bkt_logo_83x80.bmp,setup\bkt_logo_110x106.bmp,setup\bkt_logo_138x140.bmp
UninstallDisplayIcon={uninstallexe}

[Languages]
;Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"; LicenseFile:"setup\license-de.txt"

[CustomMessages]
DeleteSettings=Delete all settings?
german.DeleteSettings=Alle Einstellungen löschen?

[Types]
Name: "compact"; Description: "PowerPoint-Toolbar mit Standardfeatures"
Name: "minimal"; Description: "Nur Basis-PowerPoint-Toolbar"
Name: "full"; Description: "Alle Toolbars und Features"
Name: "custom"; Description: "Benutzerdefinierte Auswahl"; Flags: iscustom

[Components]
Name: "powerpoint"; Description: "PowerPoint"; Types: full
Name: "powerpoint\toolbar"; Description: "PowerPoint Toolbar mit Extras"; Types: full compact minimal custom; Flags: fixed
Name: "powerpoint\customformats"; Description: "Benutzerdefiniere Formatvorlagen"; Types: full compact
Name: "powerpoint\quickedit"; Description: "QuickEdit Toolbar (Farbleiste)"; Types: full compact
Name: "powerpoint\consol"; Description: "Tool zum Konsolidieren und Teilen"; Types: full
Name: "powerpoint\statistics"; Description: "Statistiken für Shape-Auswahl"; Types: full
Name: "excel"; Description: "Excel"; Types: full
Name: "excel\toolbar"; Description: "Excel Toolbar (BETA)"; Types: full
Name: "excel\calc"; Description: "Sofort-Mini-Rechner"; Types: full
Name: "visio"; Description: "Visio"; Types: full
Name: "visio\toolbar"; Description: "Visio Toolbar (BETA)"; Types: full

[InstallDelete]
; NOTE: Remove files from old version before 2.7.0
Type: files; Name: "{app}\bkt\*.xaml"
Type: files; Name: "{app}\bkt\library\general.py"
Type: files; Name: "{app}\modules\contextmenu_ids.py"
Type: files; Name: "{app}\features\toolbox\dialogs\*.xaml"
Type: files; Name: "{app}\features\toolbox\popups\*.xaml"
; NOTE: Remove previous installer completely (i.e. old ipy versions)
Type: filesandordirs; Name: "{app}\installer"
; NOTE: Clear cache
Type: filesandordirs; Name: "{app}\resources\cache"
; NOTE: Remove old icons before 2.7.0
Type: files; Name: "{group}\AddIn neu registrieren.lnk"
Type: files; Name: "{group}\Config-Datei öffnen.lnk"

[Files]
Source: "bin\*"; DestDir: "{app}\bin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "bkt\*"; DestDir: "{app}\bkt"; Flags: ignoreversion recursesubdirs
Source: "features\*"; DestDir: "{app}\features"; Flags: ignoreversion recursesubdirs
Source: "installer\*"; DestDir: "{app}\installer"; Flags: ignoreversion recursesubdirs
Source: "modules\*"; DestDir: "{app}\modules"; Flags: ignoreversion recursesubdirs
Source: "resources\*"; DestDir: "{app}\resources"; Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "\cache\*,\settings\*,\registry\local\*,\xml\*"
Source: "documentation\example_feature_folder\*"; DestDir: "{app}\documentation\example_feature_folder"; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "CHANGELOG.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "LICENSE.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "README.md"; DestDir: "{app}"; Flags: ignoreversion
Source: "documentation\Changelog.pptx"; DestDir: "{app}\documentation"; Attribs: readonly; Flags: ignoreversion overwritereadonly uninsremovereadonly
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\BKT deinstallieren"; Filename: "{uninstallexe}"
Name: "{group}\BKT-AddIn neu registrieren"; Filename: "{app}\installer\install.bat"; WorkingDir: "{app}"
;Name: "{group}\Config-Datei öffnen"; Filename: "{app}\config.txt"
Name: "{group}\BKT-Ordner öffnen"; Filename: "{app}\"

[Run]
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install configure --migrate 2.6"; WorkingDir: "{app}\installer"; StatusMsg: "Alte Konfiguration migrieren..."; Flags: runasoriginaluser runhidden

Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install install"; WorkingDir: "{app}\installer"; StatusMsg: "Office-AddIn einrichten..."; Flags: runasoriginaluser; Components: not (excel or visio)
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install install --apps excel"; WorkingDir: "{app}\installer"; StatusMsg: "Office-AddIn einrichten..."; Flags: runasoriginaluser; Components: excel and not visio
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install install --apps visio"; WorkingDir: "{app}\installer"; StatusMsg: "Office-AddIn einrichten..."; Flags: runasoriginaluser; Components: visio and not excel
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install install --apps excel visio"; WorkingDir: "{app}\installer"; StatusMsg: "Office-AddIn einrichten..."; Flags: runasoriginaluser; Components: excel and visio

Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install configure --add_folders features\ppt_customformats"; WorkingDir: "{app}\installer"; StatusMsg: "PowerPoint Benutzerdef. Formate aktivieren..."; Flags: runasoriginaluser runhidden; Components: powerpoint\customformats
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install configure --add_folders features\ppt_consolidation_split"; WorkingDir: "{app}\installer"; StatusMsg: "PowerPoint-Konsolidierungstool aktivieren..."; Flags: runasoriginaluser runhidden; Components: powerpoint\consol
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install configure --add_folders features\ppt_quickedit"; WorkingDir: "{app}\installer"; StatusMsg: "PowerPoint-QuickEdit aktivieren..."; Flags: runasoriginaluser runhidden; Components: powerpoint\quickedit
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install configure --add_folders features\ppt_statistics"; WorkingDir: "{app}\installer"; StatusMsg: "PowerPoint-Shape-Statistics aktivieren..."; Flags: runasoriginaluser runhidden; Components: powerpoint\statistics
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install configure --add_folders features\bkt_excel"; WorkingDir: "{app}\installer"; StatusMsg: "Excel-Toolbar aktivieren..."; Flags: runasoriginaluser runhidden; Components: excel\toolbar
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install configure --add_folders features\xls_instacalc"; WorkingDir: "{app}\installer"; StatusMsg: "Excel-Mini-Rechner aktivieren..."; Flags: runasoriginaluser runhidden; Components: excel\calc
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install configure --add_folders features\bkt_visio"; WorkingDir: "{app}\installer"; StatusMsg: "Visio-Toolbar aktivieren..."; Flags: runasoriginaluser runhidden; Components: visio\toolbar
; NOTE: use flag "nowait" if problems with hanging script occure
; NOTE: use flag runhidden if DOS window should not show up

Filename: "{app}\documentation\Changelog.pptx"; Description: "Neue Funktionen und Änderungen anzeigen"; Flags: postinstall shellexec skipifsilent
  
[UninstallRun]
Filename: "{app}\bin\ipy.exe"; Parameters: "-m bkt_install uninstall"; WorkingDir: "{app}\installer"; StatusMsg: "Office-AddIn entfernen..."; Flags: runhidden; RunOnceId: "BktUninstall"

[UninstallDelete]
Type: filesandordirs; Name: "{app}\resources\cache"
Type: filesandordirs; Name: "{app}\resources\xml"

[Code]
// check if .net framework is installed before install
function InitializeSetup: Boolean;
begin
  Result := IsDotNetInstalled(net45, 0); //Returns True if .NET Framework version 4.5 is installed, or a compatible version such as 4.8
  if not Result then
    SuppressibleMsgBox(FmtMessage(SetupMessage(msgWinVersionTooLowError), ['.NET Framework', '4.5.0']), mbCriticalError, MB_OK, IDOK);
end;

// ask for delete config file and settings files during uninstall
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  case CurUninstallStep of
    usUninstall:
      begin
        if MsgBox(ExpandConstant('{cm:DeleteSettings}'), mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDYES then
          begin
            DelTree(ExpandConstant('{app}\resources\settings\*'), False, True, False);
            DelTree(ExpandConstant('{app}\config.txt'), False, True, False);
          end
      end;
  end;
end;